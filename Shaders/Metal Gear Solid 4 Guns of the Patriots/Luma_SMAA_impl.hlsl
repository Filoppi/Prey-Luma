// SMAA implementation
// See Demo for the reference https://github.com/iryoku/smaa
//
// This replaces the game's FXAA pass (0xFAB5AE7C, always active), which ran on the gamma space post process
// buffer, so both the edge detection and the neighborhood blending run in gamma space too (which is what
// SMAA's color edge detection expects anyway), and no color space conversion pass is needed.

#include "../Includes/Common.hlsl"

// float4(1 / width, 1 / height, width, height) of the pass render target (see "DrawSMAA()" in c++).
// This game has no Luma game settings cbuffer, and the post process resolution isn't guaranteed to match
// the swapchain one, so we push the render target metrics ourselves.
cbuffer SmaaMetricsCB : register(b1)
{
	float4 SmaaRtMetrics;
}

#define SMAA_RT_METRICS SmaaRtMetrics
#define SMAA_PRESET_ULTRA
#define SMAA_PREDICATION 0 // We have no depth buffer hooked up in this game
#define SMAA_CUSTOM_SL
SamplerState LinearSampler : register(s0);
SamplerState PointSampler : register(s1);
#define SMAATexture2D(tex) Texture2D tex
#define SMAATexturePass2D(tex) tex
#define SMAASampleLevelZero(tex, coord) tex.SampleLevel(LinearSampler, coord, 0)
#define SMAASampleLevelZeroPoint(tex, coord) tex.SampleLevel(PointSampler, coord, 0)
#define SMAASampleLevelZeroOffset(tex, coord, offset) tex.SampleLevel(LinearSampler, coord, 0, offset)
#define SMAASample(tex, coord) tex.Sample(LinearSampler, coord)
#define SMAASamplePoint(tex, coord) tex.Sample(PointSampler, coord)
#define SMAASampleOffset(tex, coord, offset) tex.Sample(LinearSampler, coord, offset)
#define SMAA_FLATTEN [flatten]
#define SMAA_BRANCH [branch]
#define SMAATexture2DMS2(tex) Texture2DMS<float4, 2> tex
#define SMAALoad(tex, pos, sample) tex.Load(pos, sample)
#define SMAAGather(tex, coord) tex.Gather(LinearSampler, coord, 0)
#include "../Includes/SMAA.hlsl"

Texture2D tex0 : register(t0);
Texture2D tex1 : register(t1);
Texture2D tex2 : register(t2);

void fullscreen_triangle(uint id, out float4 position, out float2 texcoord)
{
	texcoord = float2((id << 1) & 2, id & 2);
	position = float4(texcoord * float2(2.0, -2.0) + float2(-1.0, 1.0), 0.0, 1.0);
}

// SMAAEdgeDetection
//

void smaa_edge_detection_vs(uint id : SV_VertexID, out float4 position : SV_Position, out float2 texcoord : TEXCOORD0, out float4 offset[3] : TEXCOORD1)
{
	fullscreen_triangle(id, position, texcoord);
	SMAAEdgeDetectionVS(texcoord, offset);
}

float2 smaa_edge_detection_ps(float4 position : SV_Position, float2 texcoord : TEXCOORD0, float4 offset[3] : TEXCOORD1) : SV_Target
{
	// tex0 = colorTexGamma
	// (predication is disabled, so no "tex1" here)
	return SMAAColorEdgeDetectionPS(texcoord, offset, tex0);
}

//

// SMAABlendingWeightCalculation
//

void smaa_blending_weight_calculation_vs(uint id : SV_VertexID, out float4 position : SV_Position, out float2 texcoord : TEXCOORD0, out float2 pixcoord : TEXCOORD1, out float4 offset[3] : TEXCOORD2)
{
	fullscreen_triangle(id, position, texcoord);
	SMAABlendingWeightCalculationVS(texcoord, pixcoord, offset);
}

float4 smaa_blending_weight_calculation_ps(float4 position : SV_Position, float2 texcoord : TEXCOORD0, float2 pixcoord : TEXCOORD1, float4 offset[3] : TEXCOORD2) : SV_Target
{
	// tex0 = edgesTex
	// tex1 = areaTex
	// tex2 = searchTex
	return SMAABlendingWeightCalculationPS(texcoord, pixcoord, offset, tex0, tex1, tex2, 0);
}

//

// SMAANeighborhoodBlending
//

void smaa_neighborhood_blending_vs(uint id : SV_VertexID, out float4 position : SV_Position, out float2 texcoord : TEXCOORD0, out float4 offset : TEXCOORD1)
{
	fullscreen_triangle(id, position, texcoord);
	SMAANeighborhoodBlendingVS(texcoord, offset);
}

float4 smaa_neighborhood_blending_ps(float4 position : SV_Position, float2 texcoord : TEXCOORD0, float4 offset : TEXCOORD1) : SV_Target
{
	// tex0 = colorTex
	// tex1 = blendTex
	float4 color = SMAANeighborhoodBlendingPS(texcoord, offset, tex0, tex1);
	color.a = 1.0; // The original FXAA pass also always wrote out an opaque alpha
	return color;
}

//
